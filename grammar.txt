program     → declaration* EOF ;

declaration → classDecl
            | funDecl 
            | varDecl
            | statement ;

classDecl   → "class" IDENTIFIER ( "<" IDENTIFIER )? "{" ( "class"? function )* "}" ;

funDecl     → "fun" function ;

function    → IDENTIFIER "(" parameters? ")" block ;

parameters  → IDENTIFIER ( "," IDENTIFIER )* ;

varDecl     → "var" IDENTIFIER ( "=" expression )? ";" ;

statement   → exprStmt
            | forStmt
            | ifStmt
            | printStmt
            | returnStmt
            | whileStmt
            | block ;

exprStmt    → expression ";" ;
ifStmt      → "if" "(" expression ")" statement ( "else" statement )? ;
forStmt     → "for" "(" ( varDecl | exprStmt | ";" )
                        expression? ";" epxression? ";" statement;
printStmt   → "print" expression ";" ;
returnStmt  → "return" expression? ";" ;
whileStmt   → "while" "(" expression ")" statement ;
block       → "{" declaration* "}" ;

expression  → assignment ( "," assignment )* ;
assignment  → ( call "." )? IDENTIFIER "=" assignment
            | logic_or ;
logic_or    → logic_and ( "or" logic_and )* ;
logic_and   → binary_err ( "and" binary_err )* ;
binary_err  → ("!=" | "==" | ">" | ">=" | "<" | "<=" | "+" | "/" | "*") ternary
            | ternary ;
ternary     → equality ( "?" assignment ":" assignment )? ;
equality    → comparison ( ( "!=" | "==" ) comparison )* ;
comparison  → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term        → factor ( ( "-" | "+" ) factor )* ;
factor      → unary ( ( "/" | "*" ) unary )* ;
unary       → ( "!" | "-" ) unary
            | call ;
call        → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
arguments   → assignment ( "," assignment )* ;
primary     → "true" | "false" | "null" | "this"
            | NUMBER | STRING
            | "(" expression ")"
		    | IDENTIFIER ;