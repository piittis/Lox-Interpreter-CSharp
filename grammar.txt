program     → declaration* EOF ;

declaration → varDecl
            | statement ;

varDecl     → "var" IDENTIFIER ( "=" expression )? ";" ;

statement   → exprStmt
            | forStmt
            | ifStmt
            | printStmt
			| whileStmt
			| block ;

exprStmt    → expression ";" ;
ifStmt      → "if" "(" expression ")" statement ( "else" statement )? ;
forStmt     → "for" "(" ( varDecl | exprStmt | ";" )
                        expression? ";" epxression? ";" statement;
printStmt   → "print" expression ";" ;
whileStmt   → "while" "(" expression ")" statement ;
block       → "{" declaration* "}" ;

expression  → assignment ;
assignment  → IDENTIFIER "=" assignment
            | logic_or ;
logic_or    → logic_and ( "or" logic_and )* ;
logic_and   → binary_err ( "and" binary_err )* ;
binary_err  → ("!=" | "==" | ">" | ">=" | "<" | "<=" | "+" | "/" | "*") comma
            | comma ;
comma       → ternary ( "," ternary )* ;
ternary     → equality ( "?" expression ":" expression )? ;
equality    → comparison ( ( "!=" | "==" ) comparison )* ;
comparison  → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term        → factor ( ( "-" | "+" ) factor )* ;
factor      → unary ( ( "/" | "*" ) unary )* ;
unary       → ( "!" | "-" ) unary
            | primary ;
primary     → "true" | "false" | "null" | "this"
            | NUMBER | STRING
            | "(" expression ")"
		    | IDENTIFIER ;


PRECEDENCE(highest first) AND ASSOCIATIVITY:

Name	    Operators	Associates
Unary	    ! -	        Right
Factor	    / *	        Left
Term	    - +	        Left
Comparison	> >= < <=	Left
Equality	== !=	    Left